#version 460
#extension GL_ARB_shading_language_include : require

layout(local_size_x = 64) in;

#include "common/buffers.glsli"

void main() {
	const uint fragmented_pixel_edge_idx = gl_GlobalInvocationID.x;

	if (is_fragmented_pixel_edge_idx_valid(int(fragmented_pixel_edge_idx))) {
		const int sparse_pixel_edge_idx = B_fragmented_pixel_edge_attribs.data[fragmented_pixel_edge_idx].to_sparse_pixel_edge_idx;
		const int prev_fragmented_idx = B_sparse_pixel_edge_attribs.data[B_sparse_pixel_edge_attribs.data[sparse_pixel_edge_idx].prev_idx].to_fragmented_pixel_edge_idx;
		const int next_fragmented_idx = B_sparse_pixel_edge_attribs.data[B_sparse_pixel_edge_attribs.data[sparse_pixel_edge_idx].next_idx].to_fragmented_pixel_edge_idx;

		const int associated_head_idx = B_fragmented_pixel_edge_attribs.data[fragmented_pixel_edge_idx].associated_head;
		const int pixel_edge_loop_idx = B_fragmented_pixel_edge_attribs.data[associated_head_idx].to_pixel_edge_loop_idx;
		const int local_idx = B_fragmented_pixel_edge_attribs.data[fragmented_pixel_edge_idx].loop_local_idx;
		const PixelEdgeLoopAttribs loop_attribs = B_pixel_edge_loop_attribs.data[pixel_edge_loop_idx];

		const int compacted_pixel_edge_idx = loop_attribs.compacted_head_idx + local_idx;
		const int compacted_prev_idx = loop_attribs.compacted_head_idx + B_fragmented_pixel_edge_attribs.data[prev_fragmented_idx].loop_local_idx;
		const int compacted_next_idx = loop_attribs.compacted_head_idx + B_fragmented_pixel_edge_attribs.data[next_fragmented_idx].loop_local_idx;
		const int contour_pixel_idx = int(get_sparse_pixel_edge_contour_pixel_idx(sparse_pixel_edge_idx));
		const PixelEdgeOrientation orientation = get_sparse_pixel_edge_orientation(sparse_pixel_edge_idx);

		B_compacted_pixel_edge_attribs.data[compacted_pixel_edge_idx] = CompactedPixelEdgeAttribs(
			compacted_prev_idx,
			compacted_next_idx,
			contour_pixel_idx,
			orientation,
			loop_attribs.compacted_head_idx,
			pixel_edge_loop_idx,
			vec2(0),
			vec2(0),
			false,
			false,
			0,
			0
		);
	}
}
