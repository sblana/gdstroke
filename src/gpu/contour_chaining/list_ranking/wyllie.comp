#version 450
#extension GL_ARB_shading_language_include : enable

layout(local_size_x = 64) in;

#include "common/buffers.glsli"


layout(push_constant) RESTRICT uniform PushConstants {
	int  read_layer;
	int write_layer;
} U_push_constants;

void main() {
	const uint pixel_edge_idx = gl_GlobalInvocationID.x;
	if (pixel_edge_idx < B_pixel_edge_desc.buf_len_sparse_pixel_edge) {
		const SparsePixelEdgeNeighboursData neighbours = B_sparse_pixel_edge_neighbours.data[pixel_edge_idx];
		if (neighbours != SparsePixelEdgeNeighboursData(-1, -1)) {
			const SparsePixelEdgeListRankingData curr_data = B_sparse_pixel_edge_list_ranking.data[pixel_edge_idx][U_push_constants.read_layer];
			SparsePixelEdgeListRankingData next_data = curr_data;

			if (curr_data.prev_idx == -1) {
				B_sparse_pixel_edge_list_ranking.data[pixel_edge_idx][U_push_constants.write_layer] = curr_data;
				return;
			}

			const SparsePixelEdgeListRankingData prev_node_data = B_sparse_pixel_edge_list_ranking.data[curr_data.prev_idx][U_push_constants.read_layer];
			
			next_data.prev_idx = prev_node_data.prev_idx;
			next_data.value += prev_node_data.value;
			B_sparse_pixel_edge_list_ranking.data[pixel_edge_idx][U_push_constants.write_layer] = next_data;
		}
	}
}
